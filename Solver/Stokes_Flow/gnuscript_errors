set term pdf size 10,3 enhanced
set output 'errors.pdf'
set multiplot
#ERRORS
set origin 0.0,0.0
set size 0.5,1
set key bottom right
set log xy
set grid y
set format y "10^{%L}"
set xlabel offset 0,1 '{/Helvetic=10 element size}'
set ylabel offset 1,0 '{/Helvetic=10 error}'
set xtics 10 offset 0,0.5 font 'Helvetic,8'
set ytics 10 offset 0.5,0 font 'Helvetic,8'
plot[0.007:0.15][1e-6:1e-1]\
'errors' u (1./sqrt($1)):2 w lp t 'velocity',\
'errors' u (1./sqrt($1)):3 w lp t 'pressure',\
x**2/10. w l lt -1 dashtype 2 t 'x^2',\
x**1/10. w l lt -1 dashtype 3 t 'x^1'
reset
#PRESSURE
set origin 0.5,0.0
set size 0.5,1
set key top right
set grid
set xlabel offset 0,1 '{/Helvetic=10 x}'
set ylabel offset 2,0 '{/Helvetic=10 pressure}'
set xtics 0.1 offset 0,0.5 font 'Helvetic,8'
set ytics 0.05 offset 0,0 font 'Helvetic,8'
plot[0:1][-0.2:0.1]\
'Elementi.00001.txt' every ::8195::8321 u 2:9 t 'pressure' w l lc 'red',\
x*(1-x)-1/6. t 'analytical' w l dt 2 lc 'blue'
unset multiplot
